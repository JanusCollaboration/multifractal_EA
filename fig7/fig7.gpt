reset
set auto

############################
set term epslatex standalone size 8.7cm, 5.8cm font ",12" ##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "fig7.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 6


############################
# Ejes
############################
set xrange [0:0.7]
set yrange [-1.2:0.05]

set xtics (0,0.1,0.2,0.3,0.4,0.5,0.6) offset 0,0.4
set ytics (0,-0.2,-0.4,-0.6,-0.8,-1) offset 0.8,0

set xlabel "$\\alpha$" offset 0,1.2
set ylabel "$f(\\alpha)$" offset 2.1,0

############################
# Leyenda
############################
set key spacing 1.1 at 0.63, -0.7 samplen 1.5 Left width -2 reverse

############################
# Plot
############################

plot \
     "data/data_T09.txt" u 2:(-$4):3:5 w xyerror lc rgb colorGet("c9") t "$\\tau_1(q)$",\
     "data/data_T09.txt" u 6:(-$8):7:9 w xyerror pt 7 lc rgb colorGet("c7") t "$\\tau_2(q)$"

############################
# Output
############################
set out
!pdflatex fig7.tex
!rm *.tex *.log *converted* *.aux *.eps
###
