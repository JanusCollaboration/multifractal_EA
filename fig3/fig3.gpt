reset
set auto

############################
set term epslatex size 8.7cm, 5.8cm font ",12" standalone header "\\usepackage{amsmath}" ##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "fig3.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 4


############################
# Ejes
############################
set logs
set xrange [0.025:0.13]
set yrange [2.65:5.75]

set xtics ("" 0.03,"" 0.04,"0.05" 0.05,"" 0.06, "" 0.07, "" 0.08, "" 0.09,"0.1" 0.1, "" 0.2) offset 0,0.4
set ytics ("1" 1,2,3,4,5) offset 0.75,0

set xlabel "$\\overline{C_4}$" offset 0,0.85
set ylabel "$\\overline{C_4^2}/\\overline{C_4}^2$" offset 1.5,0


############################
# Leyenda
############################
set key at 0.05,3.75 samplen 1 width 1 Left reverse

############################
# Plot
############################
p \
"data/data_T09.txt" u 2:4:3:5 w xyerror ps 1 pt 9 lc rgb colorGet("c9") t "$T=0.9$",\
"data/data_T08.txt" u 2:4:3:5 w xyerror ps 1 pt 5 lc rgb colorGet("c7")  t "$T=0.8$",\
"data/data_T07.txt" u 2:4:3:5 w xyerror ps 1 pt 11 lc rgb colorGet("c10")  t "$T=0.7$",\
"data/data_Tc.txt" u 2:4:3:5 w xyerror ps 1 pt 13 lc rgb colorGet("c1")  t "$T=\\ T_\\text{c}$"

############################
# Output
############################
set out
!pdflatex fig3.tex
!rm *.tex *.log *converted* *.aux *.eps
###
