reset
set auto

############################
set term epslatex standalone size 8.7cm, 10.44cm font ",12" header "\\usepackage{amsmath}"##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "figS8.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 5
set bmargin 0

############################
# Ejes
############################
set logs
set xrange [3:24]
set yrange [1:1.6]

set xtics ("" 4,"" 5, "" 6, "" 7, "" 8, "" 9, "" 10, "" 20) offset 0,0.2
set ytics (1.1,1.3,1.5) offset 0.75,0

unset xlabel

set ylabel "$\\overline{C_4^2}/\\overline{C_4}^2$" offset 2.1,0


############################
# Leyenda
############################
set key at 20,1.58 samplen 1 maxrow 3  width -0.25 reverse spacing 1.25

############################
# Plot
############################
set multiplot layout 1,2
set origin 0,0.532
set size 1, 0.467

p \
"data/data_DIL_T09.txt" u 1:4:5 w yerror ps 1 pt 8 lc rgb colorGet("c9")  t "$\\tilde{T}=0.9$",\
"data/data_DIL_T08.txt" u 1:4:5 w yerror ps 1 pt 4 lc rgb colorGet("c7")  t "$\\tilde{T}=0.8$",\
"data/data_DIL_T07.txt" u 1:4:5 w yerror ps 1 pt 10 lc rgb colorGet("c10")  t "$\\tilde{T}=0.7$"

#Second plot
set tmargin 0
set bmargin 2
set origin 0,0
set size 1,0.532

set key at 20,3.1 samplen 1 maxrow 3  width -0.25 reverse spacing 1.25

set xlabel "$\\xi = r$" offset 0,1
set xtics ("" 4,"5" 5, "" 6, "" 7, "" 8, "" 9, "10" 10, "20" 20) offset 0,0.2
set yrange [2.5:4.5]
set ytics (2.5,3,3.5,4) offset 0.75,0

p\
"data/data_EA_T09.txt" u 1:4:5 w yerror pt 9 ps 1 lc rgb colorGet("c9") t "$T=0.9$",\
"data/data_EA_T08.txt" u 1:4:5 w yerror pt 5 ps 1 lc rgb colorGet("c7")  t "$T=0.8$",\
"data/data_EA_T07.txt" u 1:4:5 w yerror ps 1 pt 11 lc rgb colorGet("c10")  t "$T=0.7$"

unset multiplot

############################
# Output
############################
set out
!pdflatex figS8.tex
!rm *.tex *.log *converted* *.aux *.eps
###
