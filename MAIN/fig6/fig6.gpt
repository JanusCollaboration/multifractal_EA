reset
set auto

############################
set term epslatex standalone size 8.7cm, 5.8cm font ",12" ##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "fig6.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 4.5


############################
# Ejes
############################
set xrange [0:21]
#set yrange [4e-2:2e-1]

set xtics offset 0,0.4
set ytics (0.2,0.4,0.6,0.8,1,1.2) offset 0.8,0

set xlabel "$q$" offset 0,1.2
set ylabel "$\\tau(q)$" offset 2.2,0


############################
# Leyenda
############################
set key spacing 1.1 at 20, 1.12 samplen 1 Left width -2 reverse


############################
# Plot
############################
#set fit noerrorscaling

mediana=` cat data/data_T09.txt | awk '{if($3=="slope:") print $4}'`

a0=` cat data/data_T09.txt | awk '{if($2=="a0") print $4}'`

a1=` cat data/data_T09.txt | awk '{if($2=="a1") print $4}'`

b0=` cat data/data_T09.txt | awk '{if($2=="b0") print $4}'`

b1=` cat data/data_T09.txt | awk '{if($2=="b1") print $4}'`

f(x)=mediana*x*(1+a*x)/(1+b*x)
g(x)=mediana*x*(1+c*x*log(x))/((1+d*x)*(1+d*x))

a=a0;b=a1
c=b0;d=b1

set multiplot

set origin 0,0
set size 1,1

plot \
     f(x) lc rgb colorGet("c9") dt 2 lw 3 t "$\\tau_1(q)$",\
     g(x) lc rgb colorGet("c7") dt 2 lw 3 t "$\\tau_2(q)$",\
     "data/data_T09.txt" u 1:2:3 w yerror pt 1 lc rgb colorGet("c10") not


set origin 0.2,0.1
set size 0.55,0.57

set xrange[0.8:21]
set y2range[0.35:1.4]

unset xlabel
unset ylabel

set y2tics (0.4,1.2) offset -0.5,0
unset ytics
set logscale x
unset key

plot \
     f(x) axis x1y2 lc rgb colorGet("c9") dt 2 lw 3 t "Fit to $\\tau_1(x)$",\
     g(x) axis x1y2 lc rgb colorGet("c7") dt 2 lw 3 t "Fit to $\\tau_2(x)$",\
     "data/data_T09.txt" u 1:2:3 w yerror axis x1y2 pt 1 lc rgb colorGet("c10") not

unset multiplot
     
############################
# Output
############################
set out
!pdflatex fig6.tex
!rm *.tex *.log *converted* *.aux *.eps
###
