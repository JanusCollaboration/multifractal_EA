set auto

f(x)=amplitud*exp(-(x/xiexp)**powexpo)/x**powalge

powalge=0.45829
powexpo=1.41217
amplitud=0.551495
xiexp=20.5207

g(x)=(b0+b1*x+b2*x*x)*exp(-(x/2.6)**4)
b0=0.045336
b1=-0.0264096
b2=0.00558776

h(x) = f(x)+g(x)

r(x,y,z)=sqrt(x*x + y*y + z*z)

data="< cat ./data/data_T09_S000_cube_x64_y64_z64_xi20.txt"

#CUBE DATA
stat data u (0.5*log($8*$8)/log(h(r($1,$2,$3)))) nooutput

umbral= 2.

escalon(x) = (x<0)? 0 : (x<umbral)? x : umbral
hexa(x) = floor(255*(escalon(x-STATS_min)/umbral))
color(x) = hexa(x)*0x010101

!rm key.dat
set print "key.dat"
N=1000.
delta=ceil(STATS_max)/N
do for [x=0:5] {
do for [i=0:N]{
print sprintf("%d %.14g",x,i*delta)
}
}
set print

###
# SET TERMINAL
###

set term epslatex standalone size 18.5cm, 14.4cm font ",8" ##8 minimo permitido Latex
# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "fig4.tex"

###
# MULTIPLOT
###
set multiplot layout 2,4

###
# KEY
###

set size 0.1,0.5
set origin 0,0.5

set border 0
top=0.985
bottom=0.521
left=0.066
right=0.0744
set arrow 1 nohead from screen left, screen bottom to screen left, screen top front
set arrow 2 nohead from screen right, screen bottom to screen right, screen top front
set arrow 3 nohead from screen left, screen top to screen right, screen top front
set arrow 4 nohead from screen left, screen bottom to screen right, screen bottom front

unset xtics
set ytics (sprintf("%.2f",STATS_min) STATS_min,1,1.5,2,sprintf("$\\geq%.2f$",umbral+0.5) umbral+0.5) scale 0

plot [-1.5:6.5][STATS_min*0.8:umbral+0.5]"key.dat" u 1:2:(color($2)) w points pt 5 ps 1 lc rgb var not

###
# CUBO
###
reset

set size 0.42,0.5
set origin 0.08,0.5

set view 70,128, 1.2
set view equal xyz
set ticslevel 0

unset xlabel
unset ylabel
unset zlabel

unset border

unset xtics
unset ytics
unset ztics

set arrow 1 from 40,-40,20 to 40,40,20 lw 4 lc rgb "red" front nohead
set arrow 2 from -40,40,20 to 40,40,20 lw 4 lc rgb "red" front nohead

set arrow 3 from 40,-40,0 to 40,40,0 lw 4 lc rgb "green" front nohead
set arrow 4 from -40,40,0 to 40,40,0 lw 4 lc rgb "green" front nohead

set arrow 5 from 40,-40,-20 to 40,40,-20 lw 4 lc rgb "blue" front nohead
set arrow 6 from -40,40,-20 to 40,40,-20 lw 4 lc rgb "blue" front nohead

set arrow 100 from 50,-40,-40 to 50,-30,-40 lw 2 lc rgb "black" front
set arrow 101 from 50,-40,-40 to 50,-40,-30 lw 2 lc rgb "black" front
set arrow 102 from 50,-40,-40 to 60,-40,-40 lw 2 lc rgb "black" front
set label 102 "$x$" at 64,-40,-40 center
set label 100 "$y$" at 50,-26,-40 center
set label 101 "$z$" at 50,-40,-26 center

splot data." | awk '{if($3==40) print $0}'" u 1:2:3:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 7 ps 0.75 lc rgb var not,\
      data." | awk '{if($1==40) print $0}'" u 1:2:3:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 7 ps 0.75 lc rgb var not,\
      data." | awk '{if($2==40) print $0}'" u 1:2:3:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 7 ps 0.75 lc rgb var not

unset arrow
unset label

###
# PLANO ROJO
###

set size 0.42,0.5
set origin 0.5,0.5

set xlabel "$r_x$"
set ylabel "$r_y$"

set xtics scale 0
set ytics scale 0

set xrange [-41:41]
set yrange [-41:41]

set arrow 1 from -41.25,-41 to 41.25,-41 lw 5 lc rgb "red" nohead front
set arrow 2 from -41.25,41 to 41.25,41 lw 5 lc rgb "red" nohead front
set arrow 3 from -41,-41 to -41,41 lw 5 lc rgb "red" nohead front
set arrow 4 from 41,-41 to 41,41 lw 5 lc rgb "red" nohead front

i=20

plot data." | awk '{if($3==".i.") print $0}'" u 1:2:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 5 ps 1 lc rgb var not

###
# KEY
###
reset

set size 0.1,0.5
set origin 0.922,0.5

set border 0
top=0.985
bottom=0.521
left=0.924
right=0.933
set arrow 1 nohead from screen left, screen bottom to screen left, screen top front
set arrow 2 nohead from screen right, screen bottom to screen right, screen top front
set arrow 3 nohead from screen left, screen top to screen right, screen top front
set arrow 4 nohead from screen left, screen bottom to screen right, screen bottom front

unset ytics
set y2tics (sprintf("%.2f",STATS_min) STATS_min,1,1.5,2,sprintf("$\\geq%.2f$",umbral+0.5) umbral+0.5)

unset xtics

plot [-1.5:6.5][STATS_min*0.8:umbral+0.5]"key.dat" u 1:2:(color($2)) w points pt 5 ps 1 lc rgb var not

###
# KEY
###
reset

set size 0.1,0.5
set origin 0,0

set border 0
top=0.48
bottom=0.02
left=0.066
right=0.0744
set arrow 1 nohead from screen left, screen bottom to screen left, screen top front
set arrow 2 nohead from screen right, screen bottom to screen right, screen top front
set arrow 3 nohead from screen left, screen top to screen right, screen top front
set arrow 4 nohead from screen left, screen bottom to screen right, screen bottom front

unset xtics
set ytics (sprintf("%.2f",STATS_min) STATS_min,1,1.5,2,sprintf("$\\geq%.2f$",umbral+0.5) umbral+0.5) scale 0

plot [-1.5:6.5][STATS_min*0.8:umbral+0.5]"key.dat" u 1:2:(color($2)) w points pt 5 ps 1 lc rgb var not

###
# PLANO AZUL
###
reset

set size 0.42,0.5
set origin 0.08,0

set xlabel "$r_x$"
set ylabel "$r_y$"

set xtics scale 0
set ytics scale 0

set xrange [-41:41]
set yrange [-41:41]

set arrow 1 from -41.27,-41 to 41.27,-41 lw 5 lc rgb "blue" nohead front
set arrow 2 from -41.27,41 to 41.27,41 lw 5 lc rgb "blue" nohead front
set arrow 3 from -41,-41 to -41,41 lw 5 lc rgb "blue" nohead front
set arrow 4 from 41,-41 to 41,41 lw 5 lc rgb "blue" nohead front

i=-20

plot data." | awk '{if($3==".i.") print $0}'" u 1:2:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 5 ps 1 lc rgb var not

###
# PLANO VERDE
###

set size 0.42,0.5
set origin 0.5,0

set arrow 1 from -41.27,-41 to 41.27,-41 lw 5 lc rgb "green" nohead front
set arrow 2 from -41.27,41 to 41.27,41 lw 5 lc rgb "green" nohead front
set arrow 3 from -41,-41 to -41,41 lw 5 lc rgb "green" nohead front
set arrow 4 from 41,-41 to 41,41 lw 5 lc rgb "green" nohead front

i=0

plot data." | awk '{if($3==".i.") print $0}'" u 1:2:(color(0.5*log($8*$8)/log(h(r($1,$2,$3))))) w points pt 5 ps 1 lc rgb var not

###
# KEY
###
reset

set size 0.1,0.5
set origin 0.922,0

set border 0
top=0.48
bottom=0.02
left=0.924
right=0.933
set arrow 1 nohead from screen left, screen bottom to screen left, screen top front
set arrow 2 nohead from screen right, screen bottom to screen right, screen top front
set arrow 3 nohead from screen left, screen top to screen right, screen top front
set arrow 4 nohead from screen left, screen bottom to screen right, screen bottom front


unset ytics
set y2tics (sprintf("%.2f",STATS_min) STATS_min,1,1.5,2,sprintf("$\\geq%.2f$",umbral+0.5) umbral+0.5)

unset xtics

plot [-1.5:6.5][STATS_min*0.8:umbral+0.5]"key.dat" u 1:2:(color($2)) w points pt 5 ps 1 lc rgb var not

unset multiplot

############################
# Output
############################
set out
!pdflatex fig4.tex
!rm *.tex *.log *converted* *.aux *.eps
###

