reset
set auto

############################
set term epslatex size 8.7cm, 5cm font ",10" standalone header "\\usepackage{amsmath}" ##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "fig1.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 6.5


############################
# Ejes
############################
set xrange [0.05:60]
set yrange [3e-4:1]

set xtics offset -0.5,0.4
set ytics ("$10^{-3}$" 1e-3,"$10^{-2}$" 1e-2,"$10^{-1}$" 1e-1, "1" 1) offset 0.8,-0.5

set xlabel "$r$" offset 0,1.2
set ylabel "$C_4^{\\text{av}}(r)$" offset 2,0


############################
# Leyenda
############################
set key at 59,0.5 right sample 1.5 Left reverse


############################
# Plot
############################
set logscale y

set arrow 1 nohead from 20,3e-4 to 20,1 dashtype 2 lw 2 lc rgb "0x9B9B9B"

plot \
     "data/data_EA_T09_xi20.txt" u 2:3:4 w yerr ps 1 pt 9 lc rgb colorGet("c9") t "EA",\
     "data/data_DIL_T09_xi20.txt" u 2:3:4 w yerr ps 1 pt 8 lc rgb colorGet("c4") t "DIL"

############################
# Output
############################
set out
!pdflatex fig1.tex
!rm *.tex *.log *converted* *.aux *.eps
###
