reset
set auto

############################
set term epslatex size 8.7cm, 5cm font ",12" standalone header "\\usepackage{amsmath}"##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "figS7.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


set key at 57,41 sample 1.5 Left reverse

############################
# Margenes
############################
set tmargin 0.45
set rmargin 1
set lmargin 4.5
set bmargin 2

############################
# Ejes
############################
set xrange [*:60]
set yrange [0:45]

set xlabel "$r$" offset 0,1
set ylabel "$r^2C_4^{\\text{av}}(r)$" offset 1.5,0
set format x "%.f"
set xtics offset 0,0.25
unset ytics
set ytics (10,20,30,40) offset 0.8,0

set arrow 1 nohead from 20,0 to 20,45 dashtype 2 lw 2 lc rgb "0x9B9B9B"

plot \
     "data/data_EA_T09.txt" u 2:($3*$2*$2):($4*$2*$2) w yerr pt 9 ps 1 lc rgb colorGet("c9") t "EA",\
     "data/data_DIL_T09.txt" u 2:($3*$2*$2):($4*$2*$2) w yerr pt 8 ps 1.2 lc rgb colorGet("c4") t "DIL"
          
############################
# Output
############################
set out
!pdflatex figS7.tex
!rm *.tex *.log *converted* *.aux *.eps
###


