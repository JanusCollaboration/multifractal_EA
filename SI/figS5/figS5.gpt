reset
set auto

############################
set term epslatex size 8.7cm, 5.8cm font ",12" standalone header "\\usepackage{amsmath}" ##8 minimo permitido Latex

# Sobre los tamaños para PNAS:
# 9x6 cm --> Pequeño ~ una columna
# 11x11 cm --> Mediano
# 18x22 cm --> Grande

# 6pts <= font size <= 12pts

set output "figS5.tex"
############################

############################
#ColorBlind friendly palette
############################
colorSet(name, rgb) = sprintf("color_\%s = \%d", name, rgb)
colorGet(name) = value(sprintf("color_\%s", name))

eval colorSet("c1", 0x666666)
eval colorSet("c2", 0xe84b85)
eval colorSet("c3", 0x8f4427)
eval colorSet("c4", 0x4bb0b8)
eval colorSet("c5", 0x000000)
eval colorSet("c6", 0xe69f00)
eval colorSet("c7", 0x008000)
eval colorSet("c8", 0x9400d3)
eval colorSet("c9", 0xff0000)
eval colorSet("c10",0x1000b3)


############################
# Margenes
############################
set tmargin 0.45
set rmargin 0.75
set lmargin 4.5


############################
# Ejes
############################
set xrange [0:21]
set yrange [0:2.3]

set xtics offset 0,0.4
set ytics (0.5,1,1.5,2, 2.5) offset 0.8,0

set xlabel "$q$" offset 0,1.2
set ylabel "$\\tau(q)$" offset 2.2,0


############################
# Leyenda
############################
set key spacing 1.1 at 20,0.75 samplen 1.5 Left width -1.2 maxrow 3 reverse

set label 1 "$T=0.9$" at 9.75,0.89 left
set label 2 "$T=T_{\\text{c}}$" at 15.6,0.89 left

set arrow 1 nohead lw 4 from 9.75,0.8 to 13.7,0.8
set arrow 2 nohead lw 4 from 15.6,0.8 to 19.1,0.8

############################
# Plot
############################

mediana=` cat data/data_T09.txt | awk '{if($3=="slope:") print $4}'`
a0=` cat data/data_T09.txt | awk '{if($2=="a0") print $4}'`
a1=` cat data/data_T09.txt | awk '{if($2=="a1") print $4}'`
b0=` cat data/data_T09.txt | awk '{if($2=="b0") print $4}'`
b1=` cat data/data_T09.txt | awk '{if($2=="b1") print $4}'`

f(x)=mediana*x*(1+a*x)/(1+b*x)
g(x)=mediana*x*(1+c*x*log(x))/((1+d*x)*(1+d*x))

a=a0;b=a1
c=b0;d=b1

mediana_T11=` cat data/data_Tc.txt | awk '{if($3=="slope:") print $4}'`
a0=` cat data/data_Tc.txt | awk '{if($2=="a0") print $4}'`
a1=` cat data/data_Tc.txt | awk '{if($2=="a1") print $4}'`
b0=` cat data/data_Tc.txt | awk '{if($2=="b0") print $4}'`
b1=` cat data/data_Tc.txt | awk '{if($2=="b1") print $4}'`

f_T11(x)=mediana_T11*x*(1+a_T11*x)/(1+b_T11*x)
g_T11(x)=mediana_T11*x*(1+c_T11*x*log(x))/((1+d_T11*x)*(1+d_T11*x))

a_T11=a0;b_T11=a1
c_T11=b0;d_T11=b1

plot \
     f(x) lc rgb colorGet("c9") dt 2 lw 3 t "$\\tau_1(q)$",\
     g(x) lc rgb colorGet("c7") dt 2 lw 3 t "$\\tau_2(q)$",\
     "data/data_T09.txt" u 1:2:3 w yerror pt 1 ps 1 lc rgb colorGet("c10") t "data",\
     f_T11(x) lc rgb colorGet("c5") dt 2 lw 3 t "$\\tau_1(q)$",\
     g_T11(x) lc rgb colorGet("c6") dt 2 lw 3 t "$\\tau_2(q)$",\
     "data/data_Tc.txt" u 1:2:3 w yerror pt 7 ps 1 lc rgb colorGet("c8") t "data"

     
############################
# Output
############################
set out
!pdflatex figS5.tex
!rm *.tex *.log *converted* *.aux *.eps
###
